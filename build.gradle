plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.sonarqube' version '2.8'
    id 'jacoco'
    id 'java'
    id 'idea'
}

group = 'com.wheremydocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }

    integrationTestImplementation {
        extendsFrom implementation
    }
    integrationTestRuntimeOnly {
        extendsFrom runtimeOnly
    }
}

repositories {
    mavenCentral()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.RELEASE'
    }
}

sourceSets {
    'integration-test' {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.data:spring-data-envers"
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.modelmapper:modelmapper:2.3.5'
    implementation 'io.springfox:springfox-swagger2:3.0.0-SNAPSHOT'
    implementation 'io.springfox:springfox-data-rest:3.0.0-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation "org.testcontainers:testcontainers:1.12.5"
    integrationTestImplementation "org.testcontainers:postgresql:1.12.5"
}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
}


task integrationTest(type: Test) {
    systemProperty 'spring.profiles.active', 'test'
    testClassesDirs = sourceSets.'integration-test'.output.classesDirs
    classpath = sourceSets.'integration-test'.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

sonarqube {
    properties {
        property "sonar.projectKey", "wheremydocs_customer"
        property "sonar.organization", "wheremydocs"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        properties["sonar.tests"] += sourceSets.'integration-test'.allSource.srcDirs
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
    executionData(test, integrationTest)
}
